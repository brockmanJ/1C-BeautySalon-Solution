
Процедура ОбработкаПроведения(Отказ, Режим)
	
	СтруктураУчетнаяПолитика = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата);
	Если СтруктураУчетнаяПолитика.СпособУчетаЗапасов = Перечисления.СпособыСписанияЗапасов.FEFO Тогда
		ОтражатьСрокиГодности = Истина;
	Иначе
		ОтражатьСрокиГодности = Ложь;
	КонецЕсли;
	
	Движения.ТоварыНаСкладах.Записывать = Истина; 
	Движения.УчетЗатрат.Записывать = Истина; 
	Движения.Хозрасчетный.Записывать = Истина;
	
	СчетКредита = ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПоступлениеТоваровИУслугТовары.Номенклатура КАК Номенклатура,
	               |	СУММА(ПоступлениеТоваровИУслугТовары.Количество) КАК Количество,
	               |	СУММА(ПоступлениеТоваровИУслугТовары.Сумма) КАК Сумма,
	               |	ПоступлениеТоваровИУслугТовары.СрокГодности КАК СрокГодности,
	               |	NULL КАК СтатьяЗатрат,
	               |	ПоступлениеТоваровИУслугТовары.Номенклатура.СчетБухгалтерскогоУчета КАК СчетБухгалтерскогоУчета
	               |ПОМЕСТИТЬ ВТ_Товары
	               |ИЗ
	               |	Документ.ПоступлениеТоваровИУслуг.Товары КАК ПоступлениеТоваровИУслугТовары
	               |ГДЕ
	               |	ПоступлениеТоваровИУслугТовары.Ссылка = &Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПоступлениеТоваровИУслугТовары.Номенклатура,
	               |	ПоступлениеТоваровИУслугТовары.СрокГодности,
	               |	ПоступлениеТоваровИУслугТовары.Номенклатура.СчетБухгалтерскогоУчета
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ПоступлениеТоваровИУслугУслуги.Услуга,
	               |	СУММА(ПоступлениеТоваровИУслугУслуги.Количество),
	               |	СУММА(ПоступлениеТоваровИУслугУслуги.Сумма),
	               |	NULL,
	               |	ПоступлениеТоваровИУслугУслуги.СтатьяЗатрат,
	               |	ПоступлениеТоваровИУслугУслуги.Услуга.СчетБухгалтерскогоУчета
	               |ИЗ
	               |	Документ.ПоступлениеТоваровИУслуг.Услуги КАК ПоступлениеТоваровИУслугУслуги
	               |ГДЕ
	               |	ПоступлениеТоваровИУслугУслуги.Ссылка = &Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПоступлениеТоваровИУслугУслуги.Услуга,
	               |	ПоступлениеТоваровИУслугУслуги.СтатьяЗатрат,
	               |	ПоступлениеТоваровИУслугУслуги.Услуга.СчетБухгалтерскогоУчета
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_Товары.Количество КАК Количество,
	               |	ВТ_Товары.Сумма КАК Сумма,
	               |	ВТ_Товары.Номенклатура КАК Номенклатура,
	               |	ВЫБОР
	               |		КОГДА ВТ_Товары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	               |			ТОГДА ЛОЖЬ
	               |		ИНАЧЕ ИСТИНА
	               |	КОНЕЦ КАК ЭтоТовар,
	               |	ВТ_Товары.СрокГодности КАК СрокГодности,
	               |	ВТ_Товары.СтатьяЗатрат КАК СтатьяЗатрат,
	               |	ВТ_Товары.СчетБухгалтерскогоУчета КАК СчетБухгалтерскогоУчета
	               |ИЗ
	               |	ВТ_Товары КАК ВТ_Товары
	               |ИТОГИ
	               |	СУММА(Количество),
	               |	СУММА(Сумма)
	               |ПО
	               |	СтатьяЗатрат";
	
	Выборка = Запрос.Выполнить().Выбрать(); 
	ВыборкаСтатьяЗатрат = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаСтатьяЗатрат.Следующий() Цикл 
		Если ЗначениеЗаполнено(ВыборкаСтатьяЗатрат.СтатьяЗатрат) Тогда
			// Регистр "Учет затрат"
			ДвижениеЗатрат = Движения.УчетЗатрат.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеЗатрат,ВыборкаСтатьяЗатрат);
			ДвижениеЗатрат.Период = Дата;
		КонецЕсли;
		ВыборкаНоменклатура = ВыборкаСтатьяЗатрат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаНоменклатура.Следующий() Цикл
			Если ВыборкаНоменклатура.ЭтоТовар Тогда
				// Регистр "Товары на складах"
				Движение = Движения.ТоварыНаСкладах.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
				Движение.Период = Дата;
				Движение.Номенклатура = ВыборкаНоменклатура.Номенклатура;
				Движение.Склад = Склад;
				Движение.Количество = ВыборкаНоменклатура.Количество;
				Движение.Сумма = ВыборкаНоменклатура.Сумма; 
				
				Если ОтражатьСрокиГодности Тогда
					Движение.СрокГодности = ВыборкаНоменклатура.СрокГодности;
				КонецЕсли;
				
				// Регистр "Журнал бухгалтерских проводок - Товары"
				Движение = Движения.Хозрасчетный.Добавить();
				Движение.СчетДт = ВыборкаНоменклатура.СчетБухгалтерскогоУчета;
				Движение.СчетКт = СчетКредита;
				Движение.Период = Дата;
				Движение.Сумма = ВыборкаНоменклатура.Сумма;
				Движение.Содержание = "Отражено поступление товарно-материальных ценностей от поставщика";
				Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура] = ВыборкаНоменклатура.Номенклатура;
				Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты] = Контрагент;
			КонецЕсли;
			// Регистр "Журнал бухгалтерских проводок - Услуги"
			Если ЗначениеЗаполнено(ВыборкаСтатьяЗатрат.СтатьяЗатрат) Тогда
			Движение = Движения.Хозрасчетный.Добавить();
			Движение.СчетДт = ВыборкаНоменклатура.СчетБухгалтерскогоУчета;
			Движение.СчетКт = СчетКредита;
			Движение.Период = Дата;
			Движение.Сумма = ВыборкаНоменклатура.Сумма;
			Движение.Содержание = "Отражено поступление услуг от поставщика";
			БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(Движение.СчетДт, Движение.СубконтоДт, ВыборкаСтатьяЗатрат.СтатьяЗатрат);
			БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(Движение.СчетКт, Движение.СубконтоКт, Контрагент);   
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	
КонецПроцедуры
