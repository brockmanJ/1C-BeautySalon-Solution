
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПредварительнаяЗапись") Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
		
		Запрос.Текст=
		"ВЫБРАТЬ
		|	Реализация.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.Реализация КАК Реализация
		|ГДЕ
		|	Реализация.ДокументОснование = &Ссылка";
		
		РезультатЗапроса = Запрос.Выполнить();
		Выборка =  РезультатЗапроса.Выбрать();
		Если НЕ РезультатЗапроса.Пустой() Тогда
			Выборка.Следующий();
			ВызватьИсключение СтрШаблон("Для документа уже введен документ реализации %1!",Выборка.Ссылка); ;
		КонецЕсли;
		
		Контрагент = ДанныеЗаполнения.Клиент;
		Комментарий = ДанныеЗаполнения.Комментарий;
		Сотрудник = ДанныеЗаполнения.Сотрудник;
		ДокументОснование = ДанныеЗаполнения.Ссылка;
		СуммаДокумента = ДанныеЗаполнения.СуммаДокумента;
		Для Каждого ТекСтрокаУслуги Из ДанныеЗаполнения.Услуги Цикл
			НоваяСтрока = Услуги.Добавить();
			НоваяСтрока.Количество = ТекСтрокаУслуги.Количество;
			НоваяСтрока.Сумма = ТекСтрокаУслуги.Сумма;
			НоваяСтрока.Услуга = ТекСтрокаУслуги.Услуга;
			НоваяСтрока.Цена = ТекСтрокаУслуги.Цена;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ ЗначениеЗаполнено(Ответственный) Тогда
		Ответственный = ПараметрыСеанса.ТекущийПользователь; 
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	СтруктураУчетнаяПолитика = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата);
	Если СтруктураУчетнаяПолитика.СпособУчетаЗапасов = Перечисления.СпособыСписанияЗапасов.FEFO Тогда
		ОтражатьСрокиГодности = Истина;
	Иначе
		ОтражатьСрокиГодности = Ложь;
	КонецЕсли;
	
	Движения.ТоварыНаСкладах.Записывать = Истина;
	Движения.ЗаказыКлиентов.Записывать = Истина; 
	Движения.Продажи.Записывать = Истина;
	Движения.УчетЗатрат.Записывать = Истина;
	Движения.Хозрасчетный.Записывать = Истина;
	
	Движения.ТоварыНаСкладах.Записать();
	Движения.Продажи.Записать();
	Движения.УчетЗатрат.Записать();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ТоварыНаСкладах");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = Товары; 
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура"); 
	ЭлементБлокировки.УстановитьЗначение("Склад", Склад); 
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "ВЫБРАТЬ
	|	РеализацияТовары.Номенклатура КАК Номенклатура,
	|	СУММА(РеализацияТовары.Количество) КАК Количество,
	|	СУММА(РеализацияТовары.Сумма) КАК Сумма,
	|	РеализацияТовары.Номенклатура.СчетБухгалтерскогоУчета КАК СчетБухгалтерскогоУчета
	|ПОМЕСТИТЬ ВТ_Товары
	|ИЗ
	|	Документ.Реализация.Товары КАК РеализацияТовары
	|ГДЕ
	|	РеализацияТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияТовары.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияУслуги.Услуга,
	|	СУММА(РеализацияУслуги.Количество),
	|	СУММА(РеализацияУслуги.Сумма),
	|	РеализацияУслуги.Услуга.СчетБухгалтерскогоУчета
	|ИЗ
	|	Документ.Реализация.Услуги КАК РеализацияУслуги
	|ГДЕ
	|	РеализацияУслуги.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияУслуги.Услуга
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Товары.Количество КАК Количество,
	|	ВТ_Товары.Сумма КАК Сумма,
	|	ТоварыНаСкладахОстатки.СрокГодности КАК СрокГодности,
	|	ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	|	ЕСТЬNULL(ТоварыНаСкладахОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
	|	ВТ_Товары.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ВТ_Товары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЭтоТовар,
	|	ВТ_Товары.Номенклатура.Представление КАК НоменклатураПредставление,
	|	ВТ_Товары.Номенклатура.СтатьяЗатрат КАК СтатьяЗатрат,
	|	ВТ_Товары.СчетБухгалтерскогоУчета КАК СчетБухгалтерскогоУчета
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(
	|			&МоментВремени,
	|			Номенклатура В
	|				(ВЫБРАТЬ
	|					ВТ_Товары.Номенклатура
	|				ИЗ
	|					ВТ_Товары КАК ВТ_Товары)) КАК ТоварыНаСкладахОстатки
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_Товары КАК ВТ_Товары
	|		ПО ТоварыНаСкладахОстатки.Номенклатура = ВТ_Товары.Номенклатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	СрокГодности
	|ИТОГИ
	|	СУММА(Количество),
	|	СУММА(Сумма),
	|	СУММА(КоличествоОстаток)
	|ПО
	|	СтатьяЗатрат,
	|	Номенклатура";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(МоментВремени()));
	
	ВыборкаСтатьяЗатрат = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
	Пока ВыборкаСтатьяЗатрат.Следующий() Цикл
		СуммаСебестоимостиСтатьяЗатрат = 0;
		ВыборкаНоменклатура = ВыборкаСтатьяЗатрат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаНоменклатура.Следующий() Цикл 
			Если ВыборкаНоменклатура.ЭтоТовар Тогда
				СтоимостьОбщая = 0; 
				
				Превышение = ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток;
				Если Превышение > 0 Тогда
					Отказ = Истина;
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = СтрШаблон("Превышение остатка по номенклатуре ""%1""  в количестве ""%2""", ВыборкаНоменклатура.НоменклатураПредставление, Превышение);
					Сообщение.Сообщить();		
				КонецЕсли;	
				
				Если Отказ Тогда 
					Продолжить;
				КонецЕсли;
				
				ОсталосьСписать = ВыборкаНоменклатура.Количество; 
				
				ВыборкаДетальные = ВыборкаНоменклатура.Выбрать();
				Пока ВыборкаДетальные.Следующий() И ОсталосьСписать > 0 И ВыборкаДетальные.Количество() > 0 Цикл 
					Списываем = Мин (ВыборкаДетальные.КоличествоОстаток, ОсталосьСписать);
					
					Движение = Движения.ТоварыНаСкладах.ДобавитьРасход();
					Движение.Период = Дата;
					Движение.Номенклатура = ВыборкаДетальные.Номенклатура;
					Движение.Склад = Склад;
					Движение.СрокГодности = ВыборкаДетальные.СрокГодности;
					Движение.Количество = Списываем;  
					
					Если Списываем = ВыборкаДетальные.КоличествоОстаток Тогда 
						Движение.Сумма = ВыборкаДетальные.СуммаОстаток; 
						СуммаСписания = ВыборкаДетальные.СуммаОстаток;
					Иначе
						СуммаСписания = Списываем / ВыборкаДетальные.КоличествоОстаток * ВыборкаДетальные.СуммаОстаток;
						Движение.Сумма = Списываем / ВыборкаДетальные.КоличествоОстаток * ВыборкаДетальные.СуммаОстаток;
					КонецЕсли;
					Движение.Сумма = СуммаСписания; 
					
					ОсталосьСписать = ОсталосьСписать - Списываем; 
					СтоимостьОбщая = СтоимостьОбщая + Движение.Сумма; 
					
					//Проводка по списанию товаров и материалов Дт90 Кт10/41
					Движение = Движения.Хозрасчетный.Добавить();
					Движение.СчетДт = ПланыСчетов.Хозрасчетный.Продажи;
					Движение.СчетКт = ВыборкаДетальные.СчетБухгалтерскогоУчета;
					Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура] = ВыборкаДетальные.Номенклатура; 
					Движение.Период    = Дата;    
					Движение.Сумма = СуммаСписания;
					Движение.Содержание    = "Списана себестоимость товарно-материальных ценностей";
					
				КонецЦикла; 
				
				Если ОсталосьСписать > 0 Тогда
					Движение = Движения.ТоварыНаСкладах.ДобавитьРасход();
					Движение.Период = Дата;
					Движение.Номенклатура = ВыборкаДетальные.Номенклатура;
					Движение.Склад = Склад;
					Движение.СрокГодности = Неопределено;
					Движение.Количество = ОсталосьСписать;
					Движение.Сумма = 0;
				КонецЕсли;
				
				СуммаСебестоимостиСтатьяЗатрат = СуммаСебестоимостиСтатьяЗатрат + СтоимостьОбщая;
				
			КонецЕсли;
			Движение = Движения.Продажи.Добавить(); 
			Движение.Период = Дата;
			Движение.Номенклатура = ВыборкаНоменклатура.Номенклатура;
			Движение.Сотрудник = Сотрудник;
			Движение.Контрагент = Контрагент;
			Движение.Сумма = ВыборкаНоменклатура.Сумма;	
		КонецЦикла;
		Если СуммаСебестоимостиСтатьяЗатрат > 0 Тогда	
			Движение = Движения.УчетЗатрат.Добавить(); 
			Движение.Период = Дата;
			Движение.СтатьяЗатрат = ВыборкаСтатьяЗатрат.СтатьяЗатрат;
			Движение.Сумма = СуммаСебестоимостиСтатьяЗатрат;
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		Движение = Движения.ЗаказыКлиентов.Добавить(); 
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Клиент = Контрагент;
		Движение.ЗаказКлиента = ДокументОснование;
		Движение.Сумма = Услуги.Итог("Сумма");	
	КонецЕсли;	
	
	//Проводка по отражению выручки Дт62 Кт90
	Движение = Движения.Хозрасчетный.Добавить();
	Движение.СчетДт = ПланыСчетов.Хозрасчетный.РасчетыСПокупателями;
	Движение.СчетКт = ПланыСчетов.Хозрасчетный.Продажи;
	БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(Движение.СчетДт, Движение.СубконтоДт, Контрагент);
	Движение.Период = Дата;
	Движение.Сумма = СуммаДокумента;
	Движение.Содержание = "Отражена выручка от реализации товаров и услуг";
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		ЗаписьОбъект = ДокументОснование.ПолучитьОбъект();
		ЗаписьОбъект.УслугаОказана = Истина;
		ЗаписьОбъект.Записать();
	КонецЕсли;
КонецПроцедуры
