
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ТипДокументаОснования = ТипЗнч(ДанныеЗаполнения);
	
	ТипДокументаРеализация				      		  =  Тип("ДокументСсылка.Реализация");
	ТипДокументаПоступлениеТоваровИУслуг 			  =  Тип("ДокументСсылка.ПоступлениеТоваровИУслуг");
	
	ДанныеЗаполненияОснования = Новый Структура;
	Если ТипДокументаОснования = ТипДокументаРеализация Тогда
		ДанныеЗаполненияОснования  = ПолучитьДанныеПоРеализации(ДанныеЗаполнения);
	ИначеЕсли ТипДокументаОснования = ТипДокументаПоступлениеТоваровИУслуг Тогда 
		ДанныеЗаполненияОснования = ПолучитьДанныеПоПоступлениюТоваровИУслуг(ДанныеЗаполнения);
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполненияОснования);
КонецПроцедуры 

Функция ПолучитьДанныеПоРеализации(ДанныеЗаполнения) 
	
	Запрос = Новый Запрос; 
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);    
	Запрос.Текст=
	"ВЫБРАТЬ
	|    Реализация.Контрагент КАК Получатель,
	|    Реализация.СуммаДокумента КАК СуммаДокумента,
	|    Реализация.Договор КАК Договор,
	|    Реализация.Ссылка КАК ДокументОснование,
	|    ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходаДенег.ВозвратПокупателю) КАК ВидОперации
	|ИЗ
	|    Документ.Реализация КАК Реализация
	|ГДЕ
	|    Реализация.Ссылка = &Ссылка";
	
	Выборка =  Запрос.Выполнить().Выбрать();
	Выборка.Следующий(); 
	
	Возврат Выборка;
	
КонецФункции 

Функция ПолучитьДанныеПоПоступлениюТоваровИУслуг(ДанныеЗаполнения) 
	
	Запрос = Новый Запрос; 
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	Запрос.Текст=
	"ВЫБРАТЬ
	|    ПоступлениеТоваровИУслуг.Контрагент КАК Получатель,
	|    ПоступлениеТоваровИУслуг.Договор КАК Договор,
	|    ПоступлениеТоваровИУслуг.СуммаДокумента КАК СуммаДокумента,
	|    ПоступлениеТоваровИУслуг.Ссылка КАК ДокументОснование,
	|    ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходаДенег.ОплатаПоставщику) КАК ВидОперации
	|ИЗ
	|    Документ.ПоступлениеТоваровИУслуг КАК ПоступлениеТоваровИУслуг
	|ГДЕ
	|    ПоступлениеТоваровИУслуг.Ссылка = &Ссылка";
	
	Выборка =  Запрос.Выполнить().Выбрать();
	Выборка.Следующий(); 
	
	Возврат Выборка;
	
КонецФункции

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ ЗначениеЗаполнено(Ответственный) Тогда
		Ответственный = ПараметрыСеанса.ТекущийПользователь; 
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	Движения.ДенежныеСредства.Записывать = Истина;
	Движение = Движения.ДенежныеСредства.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
	Движение.Период = Дата;
	Движение.Касса = Касса;
	Движение.Сумма = СуммаДокумента; 
	
	Движения.Хозрасчетный.Записывать = Истина;
	
	АналитикаПроводки = ПолучитьАналитикуПроводки();
	
	Движение = Движения.Хозрасчетный.Добавить();
	Движение.СчетДт = АналитикаПроводки.СчетДебета;
	Движение.СчетКт = АналитикаПроводки.СчетКредита;
	Движение.Период = Дата;
	Движение.Сумма = СуммаДокумента;
	Движение.Содержание = АналитикаПроводки.СодержаниеОперации;
	БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(Движение.СчетДт, Движение.СубконтоДт, АналитикаПроводки.СубконтоДебет);
	БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(Движение.СчетКт, Движение.СубконтоКт, АналитикаПроводки.СубконтоКредит);
	
КонецПроцедуры 

Функция ПолучитьАналитикуПроводки()
    
    ОплатаПоставщику            	= Перечисления.ВидыОперацийРасходаДенег.ОплатаПоставщику;
    ВозвратПокупателю             	= Перечисления.ВидыОперацийРасходаДенег.ВозвратПокупателю;
    ВыдачаПодОтчетнику 				= Перечисления.ВидыОперацийРасходаДенег.ВыдачаПодОтчетнику;
    ВыдачаЗаработнойПлаты     		= Перечисления.ВидыОперацийРасходаДенег.ВыдачаЗаработнойПлаты;
    
    СтруктураАналитики = Новый Структура;
    СтруктураАналитики.Вставить("СчетКредита", ПланыСчетов.Хозрасчетный.Касса);
    СтруктураАналитики.Вставить("СубконтоКредит", Касса);
    Если ВидОперации = ОплатаПоставщику Тогда
        СтруктураАналитики.Вставить("СчетДебета", ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками);
        СтруктураАналитики.Вставить("СубконтоДебет", Получатель);
        СтруктураАналитики.Вставить("СодержаниеОперации", "Оплата поставщику наличными из кассы");
    ИначеЕсли ВидОперации = ВозвратПокупателю Тогда
        СтруктураАналитики.Вставить("СчетДебета", ПланыСчетов.Хозрасчетный.РасчетыСПокупателями);    
        СтруктураАналитики.Вставить("СубконтоДебет", Получатель);
        СтруктураАналитики.Вставить("СодержаниеОперации", "Возврат покупателю наличными из кассы");
    ИначеЕсли ВидОперации = ВыдачаПодОтчетнику Тогда 
        СтруктураАналитики.Вставить("СчетДебета", ПланыСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами);
        СтруктураАналитики.Вставить("СубконтоДебет", Получатель);
        СтруктураАналитики.Вставить("СодержаниеОперации", "Выдача денежных средств подотчетному лицу");
    ИначеЕсли ВидОперации = ВыдачаЗаработнойПлаты Тогда
        СтруктураАналитики.Вставить("СчетДебета", ПланыСчетов.Хозрасчетный.РасчетыСПерсоналом);
        СтруктураАналитики.Вставить("СубконтоДебет", Получатель);
        СтруктураАналитики.Вставить("СодержаниеОперации", "Выплата заработной платы наличными из кассы");        
    КонецЕсли;
    
    Возврат СтруктураАналитики;
    
КонецФункции
