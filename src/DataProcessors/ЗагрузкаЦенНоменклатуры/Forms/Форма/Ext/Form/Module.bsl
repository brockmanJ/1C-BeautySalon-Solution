&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЭтаФорма.ДатаПрайсЛиста = ТекущаяДата();
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьПрайсЛист(Команда)
	СкопироватьФайлНаСервер("ПостроительЗапросов");
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьФайлНаСервер(СпособЗагрузки)
	ОповещениеОЗавершении = Новый ОписаниеОповещения("СкопироватьФайлНаСерверЗавершение", ЭтотОбъект, СпособЗагрузки);
	НачатьПомещениеФайлаНаСервер(ОповещениеОЗавершении,,,,,УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьФайлНаСерверЗавершение(ОписаниеПомещенногоФайла, ДополнительныеПараметры) Экспорт
	Если ОписаниеПомещенногоФайла <> Неопределено Тогда
		АдресФайлаВХранилище = ОписаниеПомещенногоФайла.Адрес;
		ЗагрузитьИзТабличногоДокументаНаСервере(АдресФайлаВХранилище, ДатаПрайсЛиста, ВидЦеныПродажи, ДополнительныеПараметры);
	КонецЕсли;
КонецПроцедуры


&НаСервереБезКонтекста
Процедура ЗагрузитьИзТабличногоДокументаНаСервере(АдресФайлаВХранилище, ДатаПрайсЛиста, ВидЦеныПродажи, ДополнительныеПараметры)
	ТабДок = ПрочитатьФайл(АдресФайлаВХранилище);
	
	ТаблицаПрайсЛиста = ЗаполнитьТаблицуЗначенийЧерезПостроительЗапроса(ТабДок);
	
	Если ТаблицаПрайсЛиста.Количество() Тогда
		ЗагрузитьПрайсЛистПостащика(ТаблицаПрайсЛиста, ДатаПрайсЛиста, ВидЦеныПродажи);
	Иначе
		ОбщегоНазначения.СообщитьПользователю("Выбранный файл не содержит строк с ценами!");
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПрочитатьФайл(АдресФайлаВХранилище) 
	
	ТабДок = Новый ТабличныйДокумент;
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".xlsx");
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресФайлаВХранилище);
	ДвоичныеДанные.Записать(ИмяВременногоФайла);
	
	Попытка
		ТабДок.Прочитать(ИмяВременногоФайла);
	Исключение
		ВызватьИсключение "Не удалось прочитать файл EXCEL в табличный документ!";
	КонецПопытки;
	
	Возврат ТабДок;
	
КонецФункции 

&НаСервереБезКонтекста
Процедура ЗагрузитьПрайсЛистПостащика(ТаблицаПрайсЛиста, ДатаПрайсЛиста, ВидЦеныПродажи)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаПрайсЛиста", ТаблицаПрайсЛиста);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ТаблицаПрайсЛиста.Код КАК Код,
	|	ТаблицаПрайсЛиста.Цена КАК Цена
	|ПОМЕСТИТЬ ВТ_ТаблицаПрайсЛиста
	|ИЗ
	|	&ТаблицаПрайсЛиста КАК ТаблицаПрайсЛиста
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Ном.Ссылка КАК Номенклатура,
	|	ВТ_ТаблицаПрайсЛиста.Цена КАК Цена
	|ИЗ
	|	ВТ_ТаблицаПрайсЛиста КАК ВТ_ТаблицаПрайсЛиста
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Ном
	|		ПО ВТ_ТаблицаПрайсЛиста.Код = Ном.Код";
	
	Рез = Запрос.Выполнить();
	ТаблицаРезультатаЗапроса = Рез.Выгрузить();
	Если Рез.Пустой() Тогда
		ОбщегоНазначения.СообщитьПользователю("Выбранный файл не содержит строк с кодом номенклатуры");
		Возврат;
	ИначеЕсли  ТаблицаПрайсЛиста.Количество() > ТаблицаРезультатаЗапроса.Количество() Тогда
		ОбщегоНазначения.СообщитьПользователю("Найдена не вся номенклатура по коду из файла!");
		Возврат;
	КонецЕсли;
	
	Выборка = Рез.Выбрать();
	
	КоличествоВсего = Выборка.Количество();
	КоличествоЗаписанных = 0;
	Пока Выборка.Следующий() Цикл
		РегистрУстановкиЦен = РегистрыСведений.ЦеныНоменклатуры.СоздатьМенеджерЗаписи();
		РегистрУстановкиЦен.ВидЦены = ВидЦеныПродажи;
		РегистрУстановкиЦен.Период = ДатаПрайсЛиста;
		РегистрУстановкиЦен.Номенклатура = Выборка.Номенклатура;
		РегистрУстановкиЦен.Цена = Выборка.Цена;
		
		Попытка
			РегистрУстановкиЦен.Записать();
			КоличествоЗаписанных = КоличествоЗаписанных + 1;	
		Исключение	
			ОбщегоНазначения.СообщитьПользователю("Произошла ошибка при записи регистра Цены номенклатуры!");
		КонецПопытки;
	КонецЦикла;
	ОбщегоНазначения.СообщитьПользователю(СтрШаблон("Загружено %1 из %2 строк записи в регистр Цены номенклатуры!",КоличествоЗаписанных, КоличествоВсего));
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаполнитьТаблицуЗначенийЧерезПостроительЗапроса(ТабДок) 
	ОбластиТабличногоДокумента = ТабДок.Область(1,1, ТабДок.ВысотаТаблицы, ТабДок.ШиринаТаблицы);
	
	Построитель = Новый ПостроительЗапроса;
	Построитель.ИсточникДанных = Новый ОписаниеИсточникаДанных(ОбластиТабличногоДокумента);
	Построитель.Выполнить();
	
	ТаблицаПрайсЛиста = Построитель.Результат.Выгрузить();
	
	Возврат ТаблицаПрайсЛиста;
КонецФункции

